find_program(git_executable NAMES git git.exe git.cmd)
if(NOT git_executable)
    message(FATAL_ERROR "Failed to find git.")
endif()

function(UpdateExternalLib name url rev)
    if (EXISTS ${CMAKE_CURRENT_SOURCE_DIR}/${name})
        return()
    endif()
    set(need_checkout FALSE)
    set(external_folder "${CAIRO_CMAKE_SRC_DIR}/ext")
    set(external_lib_folder "${external_folder}/${name}")
    if(EXISTS ${external_lib_folder})
        message(STATUS "Updating ${name} to revision ${rev}...")
        execute_process(COMMAND "${git_executable}" "fetch" "origin" WORKING_DIRECTORY "${external_lib_folder}")
        execute_process(COMMAND "${git_executable}" "rev-parse" "HEAD" WORKING_DIRECTORY "${external_lib_folder}" OUTPUT_VARIABLE head_rev)
        string(STRIP ${head_rev} head_rev)
        if (${head_rev} STREQUAL ${rev})
            set(need_checkout FALSE)
        else()
            set(need_checkout TRUE)
        endif()
    else()
        message(STATUS "Cloning ${name} revision...")
        execute_process(COMMAND "${git_executable}" "clone" ${url} "-n" WORKING_DIRECTORY "${external_folder}")
        set(need_checkout TRUE)
    endif()
    if(need_checkout)
        message(STATUS "Checking out to revision ${rev}...")
        execute_process(COMMAND "${git_executable}" "checkout" "-q" ${rev} WORKING_DIRECTORY "${external_lib_folder}")
    endif()
endfunction()

UpdateExternalLib("cairo" "https://github.com/freedesktop/cairo.git" "f93fc72c0c3e158982740015304389192ce8a567")
UpdateExternalLib("pixman" "https://gitlab.freedesktop.org/pixman/pixman.git" "364760cd3d9c62088cbe6a81d1f7704344203e40")
UpdateExternalLib("zlib" "https://github.com/madler/zlib.git" "cacf7f1d4e3d44d871b605da3b647f07d718623f")
UpdateExternalLib("libpng" "https://github.com/glennrp/libpng.git" "301f7a14295a3bdfaf406dbb5004d0784dc137ea")
UpdateExternalLib("freetype2" "https://github.com/aseprite/freetype2.git" "fbbcf50367403a6316a013b51690071198962920")
